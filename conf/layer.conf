# It really depends on order of the layers appearing in BBLAYERS
# variable in toplevel bblayers.conf file, where bitbake will search
# for .inc files and others where bitbake uses BBPATH since it will
# search the directories from first to last as specified in BBPATH
# Therefore if you want a given layer to be considered high priority
# for the .inc and .conf etc. then consider it adding at the beginning
# of BBPATH. For bblayers bitbake will use BBFILES_PRIORITY to resolve
# the recipe contention so the order of directories in BBFILES does
# not matter.

# We have a conf and classes directory, append to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have a recipes directory, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "qt5-layer"
BBFILE_PATTERN_qt5-layer := "^${LAYERDIR}/"

# Define the priority for recipes (.bb files) from this layer,
# choosing carefully how this layer interacts with all of the
# other layers.

BBFILE_PRIORITY_qt5-layer = "7"

# This should only be incremented on significant changes that will
# cause compatibility issues with other layers
LAYERVERSION_qt5-layer = "1"

LAYERDEPENDS_qt5-layer = "core"

LAYERSERIES_COMPAT_qt5-layer = "sumo thud warrior"

LICENSE_PATH += "${LAYERDIR}/licenses"

IMAGE_FEATURES[validitems] += "qtcreator-debug"

QT_GIT_PROJECT ?= "qt"
QT_GIT ?= "git://code.qt.io/${QT_GIT_PROJECT}"
QT_GIT_PROTOCOL ?= "git"
QT_EDITION ?= "opensource"
